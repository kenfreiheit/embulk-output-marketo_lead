plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "java"
}

import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://dl.bintray.com/embulk-base-restclient/maven"
    }
}
configurations {
    provided
}
version = "0.1.0"
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile  "org.glassfish.jersey.core:jersey-client:2.25.1"
    compile  "org.embulk:embulk-core:0.8.+"
    compile  "org.embulk.base.restclient:embulk-base-restclient:0.5.5"
    compile  "org.embulk.base.restclient:embulk-util-retryhelper-jetty92:0.5.5"
    compile  "org.embulk.base.restclient:embulk-util-retryhelper-jaxrs:0.5.5"
    compile  "net.jodah:failsafe:1.1.0"
    provided "org.embulk:embulk-core:0.8.+"

    testCompile 'org.embulk:embulk-core:0.8.15:tests'
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
    scriptArgs "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

// It has no gemPush because it's just an example. It should not be uploaded to rubygems.

task "package"(dependsOn: ["gemspec", "classpath"]) << {
    println "> Build succeeded."
    println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["AGREX INC."]
  spec.summary       = %[Marketo Lead output plugin for Embulk]
  spec.description   = %[Upsert Lead to Marketo.]
  spec.email         = ["kentarou_yoshimura@agrex.co.jp"]
  spec.licenses      = ["Apache-2.0"]
  spec.homepage      = "https://github.com/kenfreiheit/embulk-output-marketo_lead"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['>= 10.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
